# - Config file for the SofaFramework package
# It defines the following variables
#  SofaFramework_INCLUDE_DIRS - include directories for SofaFramework
#  SofaFramework_LIBRARIES    - libraries to link against

if(POLICY CMP0074)
cmake_policy(SET CMP0074 NEW)
endif()

@PACKAGE_INIT@

list(FIND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}" HAS_SOFAFRAMEWORK_CMAKE_MODULE_PATH)

if(HAS_SOFAFRAMEWORK_CMAKE_MODULE_PATH EQUAL -1)
    list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})
endif()

# InSimo internal branch: ISSOFA_VERSION is only defined for ISSofa
# This should not be merged back to the version integrated with master
set(ISSOFA_VERSION ${SOFAFRAMEWORK_VERSION} CACHE INTERNAL BOOL )

include(CMakeSofaCreateTarget)

set_and_check(SOFAFRAMEWORK_INCLUDE_DIR  "@PACKAGE_SOFAFRAMEWORK_INCLUDE_DIR@")

if(NOT TARGET tinyxml)
    find_package(TinyXML REQUIRED)
endif()

set(SOFA_HAVE_ZLIB    "@SOFA_HAVE_ZLIB@"   CACHE INTERNAL "")
set(SOFA_HAVE_PNG     "@SOFA_HAVE_PNG@"    CACHE INTERNAL "")
set(SOFA_HAVE_GLEW    "@SOFA_HAVE_GLEW@"   CACHE INTERNAL "")
set(SOFA_HAVE_GLUT    "@SOFA_HAVE_GLUT@"   CACHE INTERNAL "")
set(SOFA_HAVE_BOOST   "@SOFA_HAVE_BOOST@"  CACHE INTERNAL "")
set(SOFA_HAVE_EIGEN2  "@SOFA_HAVE_EIGEN2@" CACHE INTERNAL "")
set(SOFA_HAVE_X11     "@SOFA_HAVE_X11@"    CACHE INTERNAL "")

if(SOFA_HAVE_ZLIB AND NOT TARGET ZLIB::ZLIB)
    find_package(ZLIB REQUIRED)
endif()

if(SOFA_HAVE_PNG AND NOT TARGET PNG::PNG)
    find_package(PNG REQUIRED)
endif()

if(SOFA_HAVE_GLEW AND NOT TARGET GLEW::GLEW)
    find_package(GLEW REQUIRED)
endif()

if(SOFA_HAVE_GLUT AND NOT TARGET GLUT::GLUT)
    find_package(GLUT REQUIRED)
endif()

if(SOFA_HAVE_X11)
    find_package(X11 REQUIRED)
    sofa_create_target(X11 SofaFramework  "${X11_X11_LIB}" "${X11_INCLUDE_DIR}" )
endif()

if(SOFA_HAVE_BOOST AND (NOT TARGET Boost::thread OR NOT TARGET Boost::system OR NOT TARGET Boost::date_time OR NOT TARGET Boost::chrono))
    find_package(Boost REQUIRED COMPONENTS thread system date_time chrono QUIET)
endif()

if( NOT TARGET SofaCore )
	include("${CMAKE_CURRENT_LIST_DIR}/SofaFrameworkTargets.cmake")
endif()

check_required_components(SofaHelper SofaDefaultType SofaCore)

set(SofaFramework_LIBRARIES SofaHelper SofaDefaultType SofaCore CACHE INTERNAL "")
set(SofaFramework_INCLUDE_DIRS ${SOFAFRAMEWORK_INCLUDE_DIR} ${SOFAFRAMEWORK_DEPENDENCY_INCLUDE_DIR} CACHE INTERNAL "")
